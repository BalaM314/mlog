#Overkill RC
#by $authors
#Memory cell layout: [enabled, load, unitId, destX, destY, shootX, shootY, shouldFire, approachDistance, secondaryUnitId, healing, overrange]
print "$name by $authors"
print "Source code available at $githubUrl"
print "Movement module"
set minHealth 0.8
set unitOverride null
print "Rest of settings in micro proc"
set quickLoops 80
set logicItemTakeRadius 5

get_wait_time:
	op add pos @thisx @thisy
	op mod pos 20
	op div waitTime pos 200
	op add waitTime 0.02


#null check for schem blocks
restart:
jump restart equal sorter1 null
jump restart equal cell1 null
set bombingItemSorter sorter1
set cell cell1

#Read things from the memory cell
init_read_cell:
	wait waitTime #Without this, the movement processors would be binding the same units in the same order, therefore being useless.
	read enabled cell $memcellpos.enabled
	read loading cell $memcellpos.loading
	read unit.id cell $memcellpos.unitId #might be a problem in multiUnit control, either make new vars or make this var dynamic
	read dest.x cell $memcellpos.destX
	read dest.y cell $memcellpos.destY
	read shoot.x cell $memcellpos.shootX
	read shoot.y cell $memcellpos.shootY
	read shouldFire cell $memcellpos.shouldFire
	read approachDistance cell $memcellpos.approachDistance
	read healing cell $memcellpos.healing
	read overrange cell $memcellpos.overrange
	jump init_read_cell equal enabled false

	get_static_vars:
		sensor bombingItem bombingItemSorter @config
		lookup unit unitType unit.id
		jump no_unit_override equal unitOverride null
		set unitType unitOverride
		no_unit_override:
		ubind unitType
		sensor unit.range
		sensor unit.itemCapacity
		sensor unit.maxHealth

set i 0
send_commands_loop:
	bind_unit:
		ubind unitType
		sensor unit.flag
		jump bind_unit notEqual unit.flag 0
	
	#if loading or healing jump there and then jump back
	jump load equal loading true
	loaded:
	jump heal equal healing true
	healed:
	
	#start movement and shooting
	send_commands:
		ucontrol approach dest.x dest.y approachDistance
		ucontrol within shoot.x shoot.y unit.range unit.targetInRange
		jump target_position equal unit.targetInRange true
		jump target_self equal overrange false

		target_overrange:
			sensor unit.x
			sensor unit.y
			op sub difference.x shoot.x unit.x #get diffrence
			op sub difference.y shoot.y unit.y
			op angle shoot.angle difference.x difference.y #normalization
			op cos shoot.normalized.x shoot.angle 0
			op sin shoot.normalized.y shoot.angle 0
			op mul shooting.x shoot.normalized.x unit.range #scale and position back
			op mul shooting.y shoot.normalized.y unit.range
			op add shooting.x unit.x
			op add shooting.y unit.y
			ucontrol target shooting.x shooting.y shouldFire
			jump end_send_commands_loop
		target_position:
			ucontrol target shoot.x shoot.y shouldFire
			jump end_send_commands_loop
		target_self:
			ucontrol targetp @unit shouldFire
			jump end_send_commands_loop


	load:
		sensor unit.firstItem
		sensor unit.totalItems
		ulocate building core false core.x core.y core.exists core
		jump take_item equal unit.firstItem null
		jump drop_item notEqual unit.firstItem bombingItem
		jump take_item notEqual unit.totalItems unit.itemCapacity
		jump loaded

	#item control
	drop_item:
		ucontrol itemDrop core 9999
	take_item:
		ucontrol move core.x core.y
		ucontrol itemTake core bombingItem 9999
		jump end_send_commands_loop always

	heal:
		sensor unit.health
		op div unit.healthPercentage unit.health unit.maxHealth
		jump healed greaterThan unit.healthPercentage minHealth
		ulocate building repair false healTurret.x healTurret.y isFound healTurret
		jump healed equal isFound false #remove this check eventually as the rc schem will have repair turret spam
		ucontrol move healTurret.x healTurret.y
		jump end_send_commands_loop
	

	end_send_commands_loop:
	op add i 1
	jump send_commands_loop lessThan i quickLoops