#Generic Worldproc Laser
#by BalaM314
#The turret that the player will be firing from
print "Generic Worldproc Turret by BalaM314"
set turret arc1
set maxRange 62.5
#Number of times the laser fires each second
set shotsPerSecond 50
#Damage dealt by each explosion per second. Note that actual damage will be several times higher than this if an enemy gets hit by multiple explosions.
set dps 100
set beamWidth 12
set ipt 250
#Distance between explosions that make up the laser.
set blocksPerExplosion 1
#Damage dealt by the projectile at its target per second.
set hitDps 1000
set hitRadius 15
set hitAir true
set hitGround true
#Coordinates at which the explosion starts
set laserStart.x @thisx
set laserStart.y @thisy
set fireAlways true

get_static_data:
	setrate ipt
	sensor turret.x
	sensor turret.y
	set dpe dps / shotsPerSecond
	set hitDpe hitDps / shotsPerSecond
	set timePerLaser 1 / shotsPerSecond
	set beamRadius beamWidth / 2

fire:
	wait_for_player:
		sensor turret.controlled
		jump wait_for_player equal turret.controlled 0
		sensor turret.shooting
		op or shoot fireAlways turret.shooting
		jump wait_for_player equal shoot false

	reset_explosion:
		set exp.x laserStart.x
		set exp.y laserStart.y

	get_shoot_pos:
		sensor turret.shootX
		sensor turret.shootY

	do_math:
		#Math to limit the range
		set shootXDiff turret.shootX - laserStart.x
		set shootYDiff turret.shootY - laserStart.y
		op len len shootYDiff shootXDiff
		op min realLen len maxRange
		set lenScl len / blocksPerExplosion
		set numExplosions realLen / blocksPerExplosion
		set vel.x shootXDiff / lenScl
		set vel.y shootYDiff / lenScl
		set instsForLoop 5 * numExplosions
		set ticksForLoop instsForLoop / ipt
		set secondsForLoop ticksForLoop / 60
		set waitTime timePerLaser - secondsForLoop

	set i 0
	fire_loop:
		op add exp.x vel.x
		op add exp.y vel.y
		explosion @sharded exp.x exp.y beamRadius dpe hitAir hitGround false
		//getblock block block exp.x exp.y
		//Maybe stop the bullet if it hits a block?
		op add i 1
		jump fire_loop lessThan i numExplosions

	hit_effect:
		explosion @sharded exp.x exp.y hitRadius hitDpe hitAir hitGround false
		#Create a cool effect when the bullet hits
		explosion @sharded exp.x exp.y hitRadius 0 false false true
	
	wait waitTime
		
jump fire
