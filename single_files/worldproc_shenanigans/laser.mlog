print "Generic Worldproc Turret by BalaM314"
set turret arc1
set maxRange 62.5
set shotsPerSecond 10
set dps 100
set projectileRadius 6
set ipt 500
set blocksPerExplosion 2
set hitDps 1000
set hitRadius 15
set hitAir true
set hitGround true
set cooldown 0.3
set laserStart.x @thisx
set laserStart.y @thisy
set fireAlways true
setrate ipt
sensor turret.x turret @x
sensor turret.y turret @y
op div dpe dps shotsPerSecond
op div hitDpe hitDps shotsPerSecond
op div timePerLaser 1 shotsPerSecond
fire:
wait_for_player:
sensor turret.controlled turret @controlled
jump wait_for_player equal turret.controlled 0
sensor turret.shooting turret @shooting
op or shoot fireAlways turret.shooting
jump wait_for_player equal shoot false
set exp.x laserStart.x
set exp.y laserStart.y
sensor turret.shootX turret @shootX
sensor turret.shootY turret @shootY
op sub shootXDiff turret.shootX laserStart.x
op sub shootYDiff turret.shootY laserStart.y
op len len shootYDiff shootXDiff
op min realLen len maxRange
op div lenScl len blocksPerExplosion
op div numExplosions realLen blocksPerExplosion
op div vel.x shootXDiff lenScl
op div vel.y shootYDiff lenScl
op mul instsForLoop 5 numExplosions
op div ticksForLoop instsForLoop ipt
op div secondsForLoop ticksForLoop 60
op sub waitTime timePerLaser secondsForLoop
set i 0
fire_loop:
op add exp.x exp.x vel.x
op add exp.y exp.y vel.y
explosion @sharded exp.x exp.y projectileRadius dpe hitAir hitGround false
op add i i 1
jump fire_loop lessThan i numExplosions
explosion @sharded exp.x exp.y hitRadius hitDpe hitAir hitGround false
explosion @sharded exp.x exp.y hitRadius 0 false false true
wait waitTime
jump fire always 0 0