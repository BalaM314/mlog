print "Generic Worldproc Turret by BalaM314"
set turret arc1
set maxRange 62.5
set shotsPerSecond 10
set dps 100
set projectileRadius 6
set ipt 500
set explosionsPerLaser 30
set hitDps 1000
set hitRadius 15
set hitAir true
set hitGround true
set cooldown 0.3
set laserStart.x @thisx
set laserStart.y @thisy
set fireAlways true
setrate ipt
sensor turret.x turret @x
sensor turret.y turret @y
op div dpe dps shotsPerSecond
op div hitDpe hitDps shotsPerSecond
op mul instsForLoop 5 explosionsPerLaser
op div ticksForLoop instsForLoop ipt
set secondsForLoop ticksForLoop
op div timePerLaser 1 shotsPerSecond
op sub waitTime timePerLaser secondsForLoop
fire:
wait_for_player:
sensor turret.controlled turret @controlled
jump wait_for_player equal turret.controlled 0
sensor turret.shooting turret @shooting
op or shoot fireAlways turret.shooting
jump wait_for_player equal shoot false
set exp.x laserStart.x
set exp.y laserStart.y
sensor turret.shootX turret @shootX
sensor turret.shootY turret @shootY
op sub shootXDiff turret.shootX exp.x
op sub shootYDiff turret.shootY exp.y
op len len shootYDiff shootXDiff
op min realLen len maxRange
op div lenScl realLen len
op mul realShootXDiff shootXDiff lenScl
op mul realShootYDiff shootYDiff lenScl
op div vel.x realShootXDiff explosionsPerLaser
op div vel.y realShootYDiff explosionsPerLaser
set i 0
fire_loop:
op add exp.x exp.x vel.x
op add exp.y exp.y vel.y
explosion @sharded exp.x exp.y projectileRadius dpe hitAir hitGround false
op add i i 1
jump fire_loop lessThan i explosionsPerLaser
explosion @sharded exp.x exp.y hitRadius hitDpe hitAir hitGround false
explosion @sharded exp.x exp.y hitRadius 0 false false true
wait waitTime
jump fire always 0 0